*** Settings ***

Resource  common.txt

Suite Setup  Open browser  ${ZOPE_URL}  ${BROWSER}  ff_profile_dir=${CURDIR}/profile
Suite Teardown  Close All Browsers

*** Keywords ***

I'm logged in as an admin
    Log in admin

I open the announcement form
    Go to  ${PLONE_URL}/@@send-announcement

I submit a test announcement
    Input text and validate  form.widgets.message  This is a test announcement!
    Click button  Send

I return to the front page
    Go to  ${PLONE_URL}

The page displays my test announcement.
    Page should contain  This is a test announcement!

I've sent a test announcement
    Go to  ${PLONE_URL}/@@send-announcement
    Input text and validate  form.widgets.message  This is a test announcement!
    Click button  Send

I submit an empty announcement
    Click button  Send

The page no longer displays my test announcement.
    Page should not contain  This is a test announcement!

*** Test Cases ***

Announcement is being delivered
    Given I'm logged in as an admin
    When I open the announcement form
      And I submit a test announcement
      And I return to the front page
    Then the page displays my test announcement.

Announcement is being cleared
    Given I'm logged in as an admin
      And I've sent a test announcement
    When I open the announcement form
      And I submit an empty announcement
      And I return to the front page
    Then the page no longer displays my test announcement.
